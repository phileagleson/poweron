[ 
 POAS.GET.LETTER.DATA.PND

-Abstract:
  This specfile returns a JSON object that contains a list fields read in from a 
  Letter File. The Letter File name to read must be passed as an Input Parameter on the form.
  The expected format of data in the Letter File is:
  
  ENVARGNUMBER1=1234
  ENVARGCHAR1=Some string to pass here
  ENVARGRATE1=123.123
  ENVARGDATE1=12/22/21
  ENVARGCODE1=123
  ENVARGMONEY1=123.00

-Details:
 The request JSON contains the following information:
  {
    "getLetterFileDataRq":{
      "FILENAME":"TEST_LETTERDATA",
      "reqTemplate":"",
      "targetId":"acctNum",
      "powerOn":"POAS.GET.LETTER.DATA.PND",
      "reqType":"POWERON",
      "fmAllowed":"false",
      "target":"Account",
      "sessionId":"xxxxxxxxxxxxx",
      "connectionId":"xxxxxxxxxxxxxxxx",
      "acctNum":"0000000000",
      "messageId":"xxxxxxxxxxxxxxxxx",
      "workAreaName":"AccountManager2"}
  }

 The response JSON contains the following information:
  {
  "getLetterFileDataRs": {
    "messageId": "xxxxxxxxxxxxxx",      
    "ENVARGNUMBER1": "1",
    "ENVARGNUMBER2": "2",
    "ENVARGNUMBER3": "3",
    "ENVARGNUMBER4": "4",
    "ENVARGNUMBER5": "5",
    "ENVARGNUMBER6": "6",
    "ENVARGNUMBER7": "7",
    "ENVARGNUMBER8": "8",
    "ENVARGNUMBER9": "9",
    "ENVARGNUMBER10": "10",
    ... rest of the fields ...    
    "status": {
      "type": "Success",
      "messages":{ 
        ""
      }
    }
]

TARGET=Account

DEFINE
 #INCLUDE "RD.GETDATA.DEF"
 #INCLUDE "RD.GETDATA1.DEF"

 [Standard .DEF POAS include files]
 #INCLUDE "RD.POAS.CONSTANTS.DEF"
 #INCLUDE "RD.POAS.GINP.GETINPUT.DEF"
 #INCLUDE "RD.POAS.JSON.CREATION.DEF"
 #INCLUDE "RD.POAS.PMLF.PARM.LETRFIL.DEF"
 #INCLUDE "RD.POAS.READ.PARSE.INPUT.DEF"
 #INCLUDE "RD.POAS.TXTP.TEXTPARSE.DEF"
 #INCLUDE "RD.POAS.VARIABLES.DEF"
 
 [ Constants ]
 FMODE="READ"
 FTYPE="LETTER"

 [ Variables ]
 X = NUMBER
 FNAME=CHARACTER
 FERROR=CHARACTER
 FNUM=NUMBER
 NUMFORMAT=CHARACTER
 BUFFER=CHARACTER
 THEFIELD=CHARACTER
 THEVALUE=CHARACTER
 
 [ Arrays ]
 ENVARGNUMBER = NUMBER ARRAY(10)
 ENVARGCHAR = CHARACTER ARRAY(10)
 ENVARGCODE = NUMBER ARRAY(10)
 ENVARGMONEY = MONEY ARRAY(10)
 ENVARGDATE = DATE ARRAY(10)
 ENVARGRATE = RATE ARRAY(10)
END

SETUP
 [Standard .SET POAS include files]
 #INCLUDE "RD.POAS.CONSTANTS.SET"
 #INCLUDE "RD.POAS.GINP.GETINPUT.SET"
 #INCLUDE "RD.POAS.JSON.CREATION.SET"
 #INCLUDE "RD.POAS.TXTP.TEXTPARSE.SET"

 [ Get the input message. ]
 CALL INITIALIZEARRAYS
 CALL POASGINPGETINPUT
 CALL POASGENLETTERINPUT
 CALL POASGETLETTERDATA

 [ Create JSON with <describe the data being output>. ]
 CALL POASGETLETTEBUILDJSON
END

PRINT TITLE=""
 SUPPRESSNEWLINE
END

PROCEDURE INITIALIZEARRAYS
 FOR X = 1 TO 10
  DO
   ENVARGNUMBER(X) = -1
   ENVARGCHAR(X) = ""
   ENVARGCODE(X) = 9999
   ENVARGMONEY(X) = $0.00
   ENVARGDATE(X) = '--/--/----'
   ENVARGRATE(X) = 000.000%
  END
END

PROCEDURE POASGENLETTERINPUT
 FOR I=1 TO INPUTCTR
  DO
   IF NAMEARRAY(I)="FILENAME" THEN
    DO
     FNAME=VALUEARRAY(I)
    END
  END
 
 IF FNAME = "" THEN
  DO
   ERRORMSGCTR=ERRORMSGCTR+1
   ERRORMSG(ERRORMSGCTR)="File Name Cannot be Blank"
   STATUSTYPE="error"
  END  
 
END


PROCEDURE POASGETLETTERDATA
  FILEOPEN(FTYPE,FNAME,FMODE,FNUM,FERROR)
  IF FERROR<>"" THEN
   DO
    ERRORMSGCTR=ERRORMSGCTR+1
    ERRORMSG(ERRORMSGCTR)="File Open Error: "+FERROR
    STATUSTYPE="error"
   END
  ELSE
   DO
    WHILE FERROR=""
     DO
      FILEREADLINE(FNUM, BUFFER, FERROR)
      IF FERROR="" THEN
       DO
        THEFIELD = SEGMENT(BUFFER,1,CHARACTERSEARCH(BUFFER,"=")-1)
        THEVALUE = SEGMENT(BUFFER,CHARACTERSEARCH(BUFFER,"=")+1,LENGTH(BUFFER))
        CALL PARSEBUFFER
       END
     END
   END  
   FILECLOSE(FNUM, FERROR)
   FILEDELETE(FTYPE,FNAME,FERROR)
END

PROCEDURE PARSEBUFFER
 FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT
    IF THEFIELD="ENVARGNUMBER"+FORMAT(NUMFORMAT,X) THEN ENVARGNUMBER(X) = VALUE(THEVALUE)
    ELSE IF THEFIELD="ENVARGCHAR"+FORMAT(NUMFORMAT,X) THEN ENVARGCHAR(X) = THEVALUE
    ELSE IF THEFIELD="ENVARGCODE"+FORMAT(NUMFORMAT,X) THEN ENVARGCODE(X) = VALUE(THEVALUE)
    ELSE IF THEFIELD="ENVARGMONEY"+FORMAT(NUMFORMAT,X) THEN ENVARGMONEY(X) = MONEY(VALUE(THEVALUE))
    ELSE IF THEFIELD="ENVARGDATE"+FORMAT(NUMFORMAT,X) THEN ENVARGDATE(X) = DATEVALUE(THEVALUE)
    ELSE IF THEFIELD="ENVARGRATE"+FORMAT(NUMFORMAT,X) THEN ENVARGRATE(X) = RATE(VALUE(THEVALUE)/100000)   
   END  
END


[
 Call the procedures required to build the response JSON.
]
PROCEDURE POASGETLETTEBUILDJSON
 CALL POASGETLETTEBUILDJSONHEADER
 IF STATUSTYPE<>STATUSTYPEERROR THEN
  DO
   CALL POASJSONCOMMA
   CALL POASGETLETTEBUILDJSONBODY
  END
 CALL POASJSONERRORMSG
 CALL POASJSONFOOTER
 CALL POASJSONOUTPUTJSON
END

[
 Create the JSON response header.
]
PROCEDURE POASGETLETTEBUILDJSONHEADER
 MAINOBJNAME="getLetterFileDataRs"
 CALL POASJSONHEADER

 POASJSONFIELD=POASGINPMESSAGEIDN
 POASJSONVALUECHAR=POASGINPMESSAGEID
 CALL POASJSONFIELDCHAR

 [ Any additional header information here ]
END

[
 Create the JSON response body.  The body contains <describe contents here>.
]
PROCEDURE POASGETLETTEBUILDJSONBODY

  FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT
    POASJSONFIELD="ENVARGNUMBER"+FORMAT(NUMFORMAT,X)
    POASJSONVALUENUM=ENVARGNUMBER(X)
    CALL POASJSONFIELDNUM
    CALL POASJSONCOMMA
   END
   
  FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT
    POASJSONFIELD="ENVARGCHAR"+FORMAT(NUMFORMAT,X)
    POASJSONVALUECHAR=ENVARGCHAR(X)
    CALL POASJSONFIELDCHAR
    CALL POASJSONCOMMA
   END
   
  FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT     
    POASJSONFIELD="ENVARGMONEY"+FORMAT(NUMFORMAT,X)
    POASJSONVALUEMONEY=ENVARGMONEY(X)
    CALL POASJSONFIELDMONEY
    CALL POASJSONCOMMA
   END
   
   FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT     
    POASJSONFIELD="ENVARGCODE"+FORMAT(NUMFORMAT,X)
    POASJSONVALUENUM=ENVARGCODE(X)
    CALL POASJSONFIELDNUM
    CALL POASJSONCOMMA
   END
   
   FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT     
    POASJSONFIELD="ENVARGDATE"+FORMAT(NUMFORMAT,X)
    POASJSONVALUEDATE=ENVARGDATE(X)
    CALL POASJSONFIELDDATE
    CALL POASJSONCOMMA
   END
      
   FOR X = 1 TO 10
   DO
    CALL GETNUMFORMAT     
    POASJSONFIELD="ENVARGRATE"+FORMAT(NUMFORMAT,X)
    POASJSONVALUERATE=ENVARGRATE(X)
    CALL POASJSONFIELDRATE
    CALL POASJSONCOMMA
   END   
   [ other variables follow ]
END

PROCEDURE GETNUMFORMAT
   IF X < 10 THEN
    NUMFORMAT = "9"
   ELSE
    NUMFORMAT = "99"
END

[Standard .PRO POAS include files]
#INCLUDE "RD.POAS.GINP.GETINPUT.PRO"
#INCLUDE "RD.POAS.JSON.CREATION.PRO"
#INCLUDE "RD.POAS.PMLF.PARM.LETRFIL.PRO"
#INCLUDE "RD.POAS.READ.PARSE.INPUT.PRO"
#INCLUDE "RD.POAS.TXTP.TEXTPARSE.PRO"
#INCLUDE "RD.POAS.USEOBJFIELD.NULL.PRO"

