[
 RD.POAS.GINP.GETINPUT.PRO

-Abstract:
 This include file contains procedures to get input arguments
 needed by the PowerOn programs.

-History:
 Proj. 17.109 Release 2018.01 05/15/2018 kschweers - Intially created.
 Proj. 12.114.11 Release 2020.00 10/31/2019 kschweers - Added new field
  for "serviceTag".

-Details:
 The PowerOn programs are driven by input argument values.  One common example
 is the Account Number for which data is to be gathered.  To indicate that an
 argument is required, the corresponding switch must be set to TRUE.

-Dependences:
 RD.GETDATA.DEF
 RD.POAS.GINP.GETINPUT.*
 RD.POAS.JSON.CREATION.*
 RD.POAS.READ.PARSE.INPUT.*
]

[
 This procedure retrieves the commonly used or "standard" input arguments.  Any
 non-standard arguments must be handled by the PowerOn program itself in a
 similar fashion.  As each is located, it is stored in its own variable.
 Required arguments (as indicated by switches) are checked for presence, and
 error messages generated for missing arguments.

 Pass:   Request Input
 Return: POASGINP* Values corresponding to the input arguments.
         JSON errors messages for any required arguments missing.
]
PROCEDURE POASGINPGETINPUT
 CALL READINPUT
 CALL PARSEINPUT

 POASSLIDLEN=GETDATANUMBER(GETPARAMIDLENGTH)

 [ Store the input arguments into their respective variable. ]
 FOR POASGINPI=0 TO INPUTCTR
  DO
   IF NAMEARRAY(POASGINPI)=POASGINPMESSAGEIDN THEN
    DO
     POASGINPMESSAGEID=VALUEARRAY(POASGINPI)
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPACCOUNTN THEN
    DO
     POASGINPACCOUNT=VALUEARRAY(POASGINPI)
     POASGINPACCOUNT=REPEATCHR("0",10-LENGTH(POASGINPACCOUNT))+POASGINPACCOUNT
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPSHAREIDN THEN
    DO
     POASGINPSHAREID=VALUEARRAY(POASGINPI)
     POASGINPSHAREID=REPEATCHR("0",POASSLIDLEN-LENGTH(POASGINPSHAREID))+
      POASGINPSHAREID
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPLOANIDN THEN
    DO
     POASGINPLOANID=VALUEARRAY(POASGINPI)
     POASGINPLOANID=REPEATCHR("0",POASSLIDLEN-LENGTH(POASGINPLOANID))+
      POASGINPLOANID
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPIDN THEN
    DO
     POASGINPID=VALUEARRAY(POASGINPI)
     POASGINPID=REPEATCHR("0",POASSLIDLEN-LENGTH(POASGINPID))+
      POASGINPID
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPINSTRTYPEN THEN
    DO
     POASGINPINSTRTYPE=VALUEARRAY(POASGINPI)
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPSTARTDATEN THEN
    DO
     POASGINPSTARTDATE=DATEVALUE(VALUEARRAY(POASGINPI))
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPENDDATEN THEN
    DO
     POASGINPENDDATE=DATEVALUE(VALUEARRAY(POASGINPI))
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPEFFDATEN THEN
    DO
     POASGINPEFFDATE=DATEVALUE(VALUEARRAY(POASGINPI))
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPEXPDATEN THEN
    DO
     POASGINPEXPDATE=DATEVALUE(VALUEARRAY(POASGINPI))
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPTRANTYPEN THEN
    DO
     POASGINPTRANTYPE=VALUEARRAY(POASGINPI)
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPREQTYPEN THEN
    DO
     POASGINPREQTYPE=VALUEARRAY(POASGINPI)
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPDESCRIPTIONN THEN
    DO
     POASGINPDESCRIPTION=VALUEARRAY(POASGINPI)
    END
   ELSE IF NAMEARRAY(POASGINPI)=POASGINPSERVICETAGN THEN
    DO
     POASGINPSERVICETAG=VALUEARRAY(POASGINPI)
    END
  END [ Loop through incoming arguments ]

 [ Edit for required arguments. ]
 IF POASGINPMESSAGEIDSW=TRUE AND POASGINPMESSAGEID="" THEN
  DO
   POASJSONMESSAGE="invalid "+POASGINPMESSAGEIDN
   CALL POASJSONMSGERROR
  END

 IF POASGINPACCOUNTSW=TRUE AND POASGINPACCOUNT="" THEN
  DO
   POASJSONMESSAGE=POASGINPACCOUNTN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPSHAREIDSW=TRUE AND POASGINPSHAREID="" THEN
  DO
   POASJSONMESSAGE=POASGINPSHAREIDN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPLOANIDSW=TRUE AND POASGINPLOANID="" THEN
  DO
   POASJSONMESSAGE=POASGINPLOANIDN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPIDSW=TRUE AND POASGINPID="" THEN
  DO
   POASJSONMESSAGE=POASGINPIDN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPINSTRTYPESW=TRUE THEN
  DO
   IF POASGINPINSTRTYPE="" THEN
    DO
     POASJSONMESSAGE=POASGINPINSTRTYPEN+POASGINPNOTBLANK
     CALL POASJSONMSGERROR
    END
   ELSE IF CHARACTERSEARCH(POASGINPINSTRTYPE,"S")=0 AND
           CHARACTERSEARCH(POASGINPINSTRTYPE,"L")=0 THEN
    DO
     POASJSONMESSAGE=POASGINPINSTRTYPEN+" must contain S or L or S,L"
     CALL POASJSONMSGERROR
    END
  END

 IF POASGINPSTARTDATESW=TRUE AND POASGINPSTARTDATE='--/--/--' THEN
  DO
   POASJSONMESSAGE=POASGINPSTARTDATEN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPENDDATESW=TRUE AND POASGINPENDDATE='--/--/--' THEN
  DO
   POASJSONMESSAGE=POASGINPENDDATEN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPEFFDATESW=TRUE AND POASGINPEFFDATE='--/--/--' THEN
  DO
   POASJSONMESSAGE=POASGINPEFFDATEN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPEXPDATESW=TRUE AND POASGINPEXPDATE='--/--/--' THEN
  DO
   POASJSONMESSAGE=POASGINPEXPDATEN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPTRANTYPESW=TRUE AND POASGINPTRANTYPE="" THEN
  DO
   POASJSONMESSAGE=POASGINPTRANTYPEN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPREQTYPESW=TRUE AND POASGINPREQTYPE="" THEN
  DO
   POASJSONMESSAGE=POASGINPREQTYPEN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPDESCRIPTIONSW=TRUE AND POASGINPDESCRIPTION="" THEN
  DO
   POASJSONMESSAGE=POASGINPDESCRIPTIONN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END

 IF POASGINPSERVICETAGSW=TRUE AND POASGINPSERVICETAG="" THEN
  DO
   POASJSONMESSAGE=POASGINPSERVICETAGN+POASGINPNOTBLANK
   CALL POASJSONMSGERROR
  END
END
